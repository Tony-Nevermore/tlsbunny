version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = 1.9

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

// tasks for running tests for GnuTLS

task buildGnutlsDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--file', 'src/main/docker/gnutls/Dockerfile',
            '--tag', 'gnutls/server/tls13',
            '.'
}

// tasks for running tests for OpenSSL

task buildOpensslDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--file', 'src/main/docker/openssl/Dockerfile',
            '--tag', 'openssl/server/tls13',
            '.'
}

task opensslHttpsClient(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.gypsyengineer.tlsbunny.tls13.test.openssl.client.HttpsClient"
}

task opensslFuzzyClient(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.gypsyengineer.tlsbunny.tls13.test.openssl.client.FuzzyClient"
}

task opensslClientAuth(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.gypsyengineer.tlsbunny.tls13.test.openssl.client.ClientAuth"
}

task opensslFuzzyClientAuth(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "com.gypsyengineer.tlsbunny.tls13.test.openssl.client.FuzzyClientAuth"
}

// tasks for running tests for picotls

task buildPicotlsDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--file', 'src/main/docker/picotls/Dockerfile',
            '--tag', 'picotls/server/tls13',
            '.'
}

task runPicotlsDockerDaemon {
    doLast {
        exec {
            ignoreExitValue = true
            commandLine 'docker', 'container', 'kill', 'picotls_server_tls13'
        }
        exec {
            ignoreExitValue = true
            commandLine 'docker', 'rm', 'picotls_server_tls13'
        }
        exec {
            commandLine 'docker',
                    'run',
                    '--name', 'picotls_server_tls13',
                    '-d',
                    '-p', '20101:20101',
                    'picotls/server/tls13'
        }
        exec {
            commandLine 'sleep', '3'
        }
    }
}

task picotlsClient(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    applicationDefaultJvmArgs = ["-Dtlsbunny.port=20101"]
    main = "com.gypsyengineer.tlsbunny.tls13.test.picotls.client.PicotlsClient"
}

task picotlsFuzzyClient(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    systemProperty 'tlsbunny.port', '20101'
    systemProperty 'tlsbunny.threads', '1'
    main = "com.gypsyengineer.tlsbunny.tls13.test.picotls.client.FuzzyClient"
}

task runPicotlsFuzzyClient {
    dependsOn 'picotlsFuzzyClient'
    dependsOn 'buildPicotlsDocker'
    dependsOn 'runPicotlsDockerDaemon'
    tasks.findByName('runPicotlsDockerDaemon').mustRunAfter 'buildPicotlsDocker'
    tasks.findByName('picotlsFuzzyClient').mustRunAfter 'runPicotlsDockerDaemon'
}
